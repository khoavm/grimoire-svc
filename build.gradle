plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.10'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com'
version = '0.0.1-SNAPSHOT'
description = 'Grimoire App'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

tasks.named('test') {
	useJUnitPlatform {
		excludeTags 'live'
	}

	// Tùy chọn: Hiển thị log rõ ràng hơn khi chạy test
	testLogging {
		events "passed", "skipped", "failed"
	}
}

tasks.register('liveTest', Test) {
	description = 'Runs integration tests that call real 3rd party APIs.'
	group = 'verification'

	// --- BẮT BUỘC PHẢI CÓ 2 DÒNG NÀY ---
	// Chỉ định vị trí file .class của test
	testClassesDirs = sourceSets.test.output.classesDirs
	// Chỉ định các thư viện cần thiết (classpath)
	classpath = sourceSets.test.runtimeClasspath
	// ------------------------------------


	useJUnitPlatform {
		includeTags 'live'
	}

	// Luôn chạy lại task này (không cache)
	outputs.upToDateWhen { false }

	testLogging {
		events "passed", "skipped", "failed"
		showStandardStreams = true
	}

	// Nếu bạn muốn chạy liveTest thì phải chắc chắn code đã compile xong
	dependsOn 'testClasses'
}


configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
    configureEach {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
}

repositories {
	mavenCentral()
	maven { url = 'https://repo.spring.io/snapshot' }
	maven { url 'https://repo.spring.io/milestone' }
}

ext {
	set('springAiVersion', "1.1.2") // Phiên bản Milestone mới nhất
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.ai:spring-ai-starter-model-google-genai'
	implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.2.0'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'
	implementation("io.micrometer:micrometer-tracing-bridge-brave")
	implementation 'io.micrometer:context-propagation:1.1.2'
	implementation 'org.apache.commons:commons-lang3:3.20.0'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
